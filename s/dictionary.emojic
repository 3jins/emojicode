🐇 🐝🐚Key ⚪️ Element ⚪🍆️ 🍇
  🖍🆕 next 🍬🐝🐚Key Element🍆
  🖍🆕 key Key
  🖍🆕 element Element
  🖍🆕 hash 🔢

  🆕 🍼key Key 🍼element Element 🍼 hash 🔢 🍇
    🤷‍♀️➡️🖍next
  🍉

  ❗️ 🐽 ➡️ Element 🍇
    ↩️ element
  🍉

  ❗️ 🐷 ele Element 🍇
    ele ➡️🖍element
  🍉

  ❗️ 🔑 ➡️ Key 🍇
    ↩️ key
  🍉


  ❗️ ⚗️ ➡️ 🔢 🍇
    ↩️ hash
  🍉

  ❗️ ⏭ ➡️ 🍬🐝🐚Key Element🍆 🍇
    ↩️ next
  🍉

  ❗️ 🥌 newEntry 🍬🐝🐚Key Element🍆 🍇
    newEntry➡️🖍next
  🍉

  ❗️ 🐻 newEntry 🐝🐚Key Element🍆 🍇
    🐕 ➡️ 🖍🆕prev
    next ➡️ 🖍🆕entry?
    🔁 entry? ➡️ entry 🍇
      entry ➡️ 🖍prev
      ⏭entry❗ ➡️ 🖍entry?
    🍉
    🥌 prev newEntry❗️
  🍉
🍉

📗
  Dictionary, holding key value pairs.

  [Read more in the
  language reference.](../../reference/the-s-package.html#-dictionaries)
📗
🌍 🐇 🍯🐚Element ⚪🍆️ 🍇
  🖍🆕 data 🧠
  🖍🆕 count 🔢
  🖍🆕 capacity 🔢

  🐇❗🛷 n 🔢 ➡️ 🔢 🍇
    ↪️ n ◀️ 7 🍇
      ↩️ 7
    🍉
    ↪️ n ◀️ 17 🍇
      ↩️ 17
    🍉
    ↪️ n ◀️ 29 🍇
      ↩️ 29
    🍉
    ↪️ n ◀️ 47 🍇
      ↩️ 47
    🍉
    ↪️ n ◀️ 167 🍇
      ↩️ 167
    🍉
    ↪️ n ◀️ 229 🍇
      ↩️ 229
    🍉
    ↪️ n ◀️ 331 🍇
      ↩️ 331
    🍉
    ↪️ n ◀️ 599 🍇
      ↩️ 599
    🍉
    ↪️ n ◀️ 1423 🍇
      ↩️ 1423
    🍉
    ↪️ n ◀️ 3221 🍇
      ↩️ 3221
    🍉
    ↪️ n ◀️ 7159 🍇
      ↩️ 7159
    🍉

    ↩️ n✖️2
  🍉

  📗 Creates an empty 🍯. 📗
  🆕 🐸 🍇
    7 ➡️ 🖍capacity
    0 ➡️ 🖍count
    ☣️ 🍇
      capacity✖️⚖️🍬🐝🐚🔡Element🍆 ➡️ length
      🆕🧠🆕 length❗️ ➡️ 🖍data
      ✍️ data 0 0 length❗
    🍉
  🍉

  🔒❓ 🔑 entry 🐝🐚🔡Element🍆 key 🔡 hash 🔢 ➡️ 👌 🍇
    ↪️ hash 🙌 ⚗️entry❗️ 🍇
      ↩️ key 🙌 🔑entry❗
    🍉
    ↩️ 👎
  🍉

  🔒❗ 🔍 key 🔡 ➡️ 🍬🐝🐚🔡Element🍆 🍇
    ⚗️key❗➡️ hash
    hash🚮capacity️ ➡️ index

    ☣️ 🍇
      🐽 data🐚🍬🐝🐚🔡Element🍆🍆 index✖️⚖️🍬🐝🐚🔡Element🍆❗ ➡️ 🖍🆕entry?
    🍉

    🔁 entry? ➡️ entry 🍇
      ↪️ 🔑🐕 entry key hash❓️ 🍇
        ↩️️ entry
      🍉
      ⏭entry❗ ➡️ 🖍entry?
    🍉

    ↩️️ 🤷‍♀️
  🍉

  📗
    Returns the value assigned to *key*. If key is not in the 🍯 no value is
    returned.
  📗
  ❗️ 🐽 key 🔡 ➡️ 🍬Element 🍇
    ↪️ 🔍🐕 key❗️ ➡️ entry 🍇
      ↩️ 🐽entry❗
    🍉
    ↩️ 🤷‍♀️
  🍉

  📗
    Removes *key* and its assigned value from the 🍯. No action is performed if
    *key* is not in the 🍯.
  📗
  ❗️ 🐨 key 🔡 🍇
    ⚗️key❗➡️ hash
    hash🚮capacity️ ➡️ index

    ☣️ 🍇
      🐽 data🐚🍬🐝🐚🔡Element🍆🍆 index✖️⚖️🍬🐝🐚🔡Element🍆❗ ➡️ 🖍🆕entry?
    🍉

    ↪️ entry? ➡️ entry 🍇
      ↪️ 🔑entry❗️️ 🙌 key 🍇
        ☣️ 🍇
          🐷 data🐚🍬🐝🐚🔡Element🍆🍆 ⏭entry❗️️ index✖️⚖️🍬🐝🐚🔡Element🍆❗
        🍉
        count ⬅️➖ 1
        ↩️️↩️️
      🍉
      entry ➡️ 🖍🆕prev
      🔁 entry? ➡️ entry 🍇
        ↪️ 🔑🐕 entry key hash❓️️ 🍇
          🥌 prev ⏭entry❗️️❗️
          count ⬅️➖ 1
          ↩️️↩️️
        🍉
        entry ➡️ 🖍prev
        ⏭entry❗ ➡️ 🖍entry?
      🍉
    🍉
  🍉

  📗 Assings a value to the provided key. 📗
  ❗️ 🐷 key 🔡 value Element 🍇
    ↪️ 🔍🐕 key❗️➡️ entry 🍇
      🐷 entry value❗️
      ↩️↩️
    🍉

    count ⬅️➕ 1
    🦕🐕❗

    ⚗️key❗➡️ hash
    hash🚮capacity️ ➡️ index
    🆕🐝🐚🔡Element🍆🆕 key value hash❗️ ➡️ entry

    ☣️ 🍇
      🐽 data🐚🍬🐝🐚🔡Element🍆🍆 index✖️⚖️🍬🐝🐚🔡Element🍆❗ ➡️ entry?
    🍉

    ↪️ entry? ➡️ anEntry 🍇
      🐻 anEntry entry❗️
      ↩️↩️
    🍉

    ☣️ 🍇
      🐷 data🐚🍬🐝🐚🔡Element🍆🍆 entry index✖️⚖️🍬🐝🐚🔡Element🍆❗
    🍉
  🍉

  🔒❗🦕️ 🍇
    ↪️ 💯count❗️➗💯capacity❗▶️ 0.75 🍇️
      data ➡️ oldData
      capacity ➡️ oldCapacity

      0 ➡️ 🖍count
      🛷🐇🍯🐚Element🍆 capacity❗️ ➡️ 🖍capacity

      ☣️ 🍇
        capacity✖️⚖️🍬🐝🐚🔡Element🍆 ➡️ length
        🆕🧠🆕 length❗️ ➡️ 🖍data
        ✍️ data 0 0 length❗
      🍉

      🔂 i 🆕⏩⏩ 0 oldCapacity❗️ 🍇
        ☣️ 🍇
          🐽 oldData 🐚🍬🐝🐚🔡Element🍆🍆 i✖️⚖️🍬🐝🐚🔡Element🍆❗ ➡️ 🖍🆕entry?
        🍉
        🔁 entry? ➡️ entry 🍇
          🐷🐕 🔑entry❗🐽entry❗❗
          ⏭entry❗ ➡️ 🖍entry?
        🍉
      🍉
    🍉
  🍉

  📗
    Returns a list consisting of all keys in this 🍯.

    >!N Note that the keys in the returned list are arbitrarily ordered.

  📗
  ❗️ 🐙 ➡️ 🍨🐚🔡🍆 🍇
    🆕🍨🐚🔡🍆🐴count❗➡️ list
    🔂 i 🆕⏩⏩ 0 capacity❗️ 🍇
      ☣️ 🍇
        🐽 data🐚🍬🐝🐚🔡Element🍆🍆 i✖️⚖️🍬🐝🐚🔡Element🍆❗ ➡️ 🖍🆕entry?
      🍉
      🔁 entry? ➡️ entry 🍇
        🐻 list 🔑entry❗❗
        ⏭entry❗ ➡️ 🖍entry?
      🍉
    🍉
    ↩️ list
  🍉

  📗
    Removes all key-value pairs in this 🍯 and returns the number of deleted
    items.
  📗
  ❗️ 🐗 ➡️ 🔢 🍇
    count ➡️ oldCount
    0 ➡️ 🖍count
    ☣️ 🍇
      ✍️ data 0 0 capacity✖️⚖️🍬🐝🐚🔡Element🍆❗
    🍉
    ↩️ oldCount
  🍉

  📗 Checks whether *key* is in this 🍯. 📗
  ❗️ 🐣 key 🔡 ➡️ 👌 🍇
    ↩️ ❎🔍🐕 key❗ 🙌 🤷‍♀️❗️
  🍉

  📗 Returns the number of items. 📗
  ❗️ 🐔 ➡️ 🔢 🍇
    ↩️ count
  🍉
🍉
