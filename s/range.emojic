📗
  Represents a range which per definition is an immutable sequence of
  numbers. [Read more in the
  language reference.](../../reference/the-s-package.html#-ranges)
📗
🌍 🕊 ⏩ 🍇
  🐊 🔂🐚🔢
  🐊 🐽️🐚🔢

  🖍🆕 start 🔢
  🖍🆕 stop 🔢
  🖍🆕 step 🔢

  📗
    Creates a range from start to stop.
  📗
  🆕 ⏩ 🍼 start 🔢 🍼 stop 🔢 🍇
    1 ➡️ 🖍step
    🍊 stop ◀️ start 🍇
      -1 ➡️ 🖍step
    🍉
  🍉

  📗
    Creates a range from start to stop with the given step.
  📗
  🆕 ⏭ 🍼 start 🔢 🍼 stop 🔢 🍼 step 🔢 🍇🍉

  📗
    Returns the `n`th element of the range. `n` is only valid if
    `start + step * n` is `>= start` and `< stop`. If `n` is invalid
    Nothingness is returned.
  📗
  ❗️ 🐽 n 🔢 ➡️ 🍬🔢 🍇
    start ➕ n ✖️ step ➡️ v
    🍊 step ◀️ 0 🍇
      🍊 stop ◀️ v 🤝 v ◀️🙌 start 🍇
        ↩️ v
      🍉
    🍉
    🍓 🍇
      🍊 start ◀️🙌 v 🤝 v ◀️ stop 🍇
        ↩️ v
      🍉
    🍉
    ↩️ 🤷‍♀️
  🍉

  📗 Returns the number of numbers in this range. 📗
  ❗️ 🐔 ➡️ 🔢 🍇
    ↩️ 🤜stop ➖ start🤛 ➗ step
  🍉

  📗 Returns an iterator to iterate over the symbols of this string. 📗
  ❗️ 🍡 ➡️ 🌳🐚🔢 🍇
    ↩️ 🆕🌳🐚🔢🆕  🐕❗️
  🍉
🍉
