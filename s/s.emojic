📗
  The s package is Emojicode’s standard library.
📗
🔮 1 0

📗
  Boolean
📗
🌍 🕊 👌 ⚪️ 🍇
  📗 Whether this value and *other* are considered equal. 📗
  🙌 other 👌 ➡️ 👌 🍇
    ↩️ 🐕 🙌 other
  🍉

  📗 Logical and. Returns true if both, the receiver and other, are true. 📗
  🤝 other 👌 ➡️ 👌 🍇
    ↩️ 🐕 🤝 other
  🍉

  📗 Logical or. Returns true if the receiver or other or both are true. 📗
  👐 other 👌 ➡️ 👌 🍇
    ↩️ 🐕 👐 other
  🍉

  📗
    Inverts the boolean. Returns true if the boolean is false, and false if the
    boolean is true.
  📗
  ❗️ ❎ ➡️ 👌 🍇
    ↩️ ❎🐕❗️
  🍉
🍉

📗
  A type conforming to this protocol provides a method 😛 to determine whether
  one of its instance is equal to an instance of type T, where T is normally the
  type itself.
📗
🌍 🐊 😛🐚T⚪️ 🍇
  📗 Whether this value and *other* are equal. 📗
  🙌 other T ➡️ 👌
🍉

🐋 👌 🍇
  🐊 😛🐚👌
🍉

📜 🔤integer.emojic🔤
📜 🔤memory.emojic🔤
📜 🔤real.emojic🔤
📜 🔤byte.emojic🔤

📗
  Symbol
📗
🌍 🕊 🔣 ⚪️ 🍇
  🐊 😛🐚🔣

  📗 Whether this value and *other* are considered equal. 📗
  🙌 other 🔣 ➡️ 👌 🍇
    ↩️ 🐕 🙌 other
  🍉

  📗 Whether this symbol represents a whitespace. 📗
  ❗️🐁 ➡️ 👌 🍇
    🔢🐕❗️ ➡️ val
    ↩️ 🤜0x9 ◀️🙌 val 🤝 val ◀️🙌 0xD🤛 👐 val 🙌 0x20 👐 val 🙌 0x85 👐
       val 🙌 0xA0 👐 val 🙌 0x1680 👐 val 🙌 0x2028 👐 val 🙌 0x2029 👐
       val 🙌 0x202F 👐 val 🙌 0x205F 👐 val 🙌 0x3000 👐 val 🙌 0xFE0F 👐
       🤜0x2000 ◀️🙌 val 🤝 val ◀️🙌 0x200A🤛
  🍉
🍉

📗
  The 🔡 class represents a string. [Read more in the language reference.](../../reference/the-s-package.html#-strings)
📗
🌍 🐇 🔡 🍇
  🖍🆕 codePoints 🧠
  🖍🆕 count 🔢
🍉

📗
  The 🍨 class represents an ordered mutable collection. [Read more in the
  language reference.](../../reference/the-s-package.html#-lists)
📗
🌍 🐇 🍨🐚Element ⚪️ 🍇
  🖍🆕 data 🧠
  🖍🆕 count 🔢
  🖍🆕 size 🔢
🍉

📗 📇 is an abstract interface to binary data. 📗
🌍 🐇 📇 🍇
  🖍🆕 data 🧠
  🖍🆕 count 🔢
🍉

📗
  The 🍯 class represents a dictionary. [Read more in the
  language reference.](../../reference/the-s-package.html#-dictionaries)
📗
🌍 🐇 🍯🐚Element ⚪️ 🍇🍉

📗
  🍡 can enumerate a collection and generates elements from the object it
  enumerates.

  To get the items from an iterator call the 🔽 method on it, until 🔽❓ returns
  👎 which indicates that there are no more items.
📗
🌍 🐊 🍡🐚Element ⚪️ 🍇
  📗
    Returns the next object. Expect undefined behavior if you call this method
    when ❓ previously returned 👎.
  📗
  ❗️ 🔽 ➡️ Element
  📗
    Whether the enumerator has more elements to provide.
  📗
  ❓ 🔽 ➡️ 👌
🍉

📗
  A type whose instances shall be enumerable using the 🔂 loop must
  implement this protocol.
📗
🌍 🐊 🔂🐚Element⚪️ 🍇
  📗
    This method must return an instance of a class that conforms to
    [[🍡]] which will be used to enumerate this instance.
  📗
  ❗️ 🍡 ➡️ 🍡🐚Element
🍉

📗
  A class whose elements can be accessed by 🐽. The number of elements must
  be finite and returned by 🐔.
📗
🌍 🐊 🐽️🐚Element⚪️ 🍇
  📗
    Gets the item at `index`. If the index is invalid Nothingness is returned.
  📗
  ❗️ 🐽 index 🔢 ➡️ 🍬Element
  📗 Returns the number of items in the list. 📗
  ❗️ 🐔 ➡️ 🔢
🍉

📗
  Iterator over a 🐽. This class is an iterator that can iterate over
  every type that conforms to the 🐽 protocol.
📗
🌍 🐇 🌳🐚Element⚪️ 🍇
  🐊 🍡🐚Element
  🐊 🔂🐚Element

  🖍🆕 iteratee 🐽🐚Element
  🖍🆕 index 🔢

  🆕 🍼 iteratee 🐽🐚Element 🍇
    0 ➡️ 🖍index
  🍉

  ❗️ 🔽 ➡️ Element 🍇
    🍺 🐽 iteratee index❗️ ➡️ sy
    index ⬅️➕ 1
    ↩️ sy
  🍉

  ❓ 🔽 ➡️ 👌 🍇
    ↩️ index ◀️ 🐔 iteratee❗️
  🍉

  ❗️ 🍡 ➡️ 🍡🐚Element 🍇
    ↩️ 🐕
  🍉
🍉

📜 🔤range.emojic🔤
📜 🔤list_iterator.emojic🔤
📜 🔤system.emojic🔤

🐋 🔢 🍇
  📗
    Creates a string representation of this integer. *base* must be greater than
    or equal to 2 and less than or equal to 35.

    The digits used to represent the integer are
    `0123456789abcdefghijklmnopqrstuvxyz`.
  📗
  ❗️ 🔡 base 🔢 ➡️ 🔡 📻 🔤sIntToString🔤

  📗 Returns the double representation of this integer. 📗
  ❗️ 💯 ➡️ 💯 🍇
    ↩️ 💯 🐕❗️
  🍉
🍉

🐋 💯 🍇
  📗
    Creates a 🔡 representation of this 💯 and the given precision – the number
    of digits after the decimal separator “.”.
  📗
  ❗️ 🔡 precision 🔢 ➡️ 🔡 📻 🔤sRealToString🔤
🍉

🐋 🔣 🍇
  📗 Returns the Unicode code point of this symbol. 📗
  ❗️ 🔢 ➡️ 🔢 📻 🔤sSymbolToInt🔤
🍉

📜 🔤prng.emojic🔤

📜 🔤string.emojic🔤
📜 🔤list.emojic🔤
📜 🔤data.emojic🔤

👴 🐋 🍯 🍇
👴   📗 Creates an empty 🍯. 📗
👴   🆕 🐸 📻 86
👴   📗
👴     Returns the value assigned to *key*. If key is not in the 🍯 ✨ is
👴     returned.
👴   📗
👴   ❗️ 🐽 key 🔡 ➡️ 🍬Element 📻 87
👴   📗
👴     Removes *key* and its assigned value from the 🍯. No action is performed if
👴     *key* is not in the 🍯.
👴   📗
👴   ❗️ 🐨 key 🔡 📻 88

👴   📗 Adds a key-value pair. 📗
👴   ❗️ 🐷 key 🔡 object Element 📻 89

👴   📗
👴     Returns a list consisting of all keys in this 🍯.

👴     >!N Note that the keys in the returned list are arbitrarily ordered.

👴   📗
👴   ❗️ 🐙 ➡️ 🍨🐚🔡 📻 90

👴   📗
👴     Removes all key-value pairs in this 🍯 and returns the number of deleted
👴     items.
👴   📗
👴   ❗️ 🐗 ➡️ 🔢 📻 91

👴   📗 Checks whether *key* is in this 🍯. 📗
👴   ❗️ 🐣 key 🔡 ➡️ 👌📻 92

👴   📗 Returns the number of items. 📗
👴   ❗️ 🐔 ➡️ 🔢📻 93
👴 🍉

📜 🔤thread.emojic🔤
